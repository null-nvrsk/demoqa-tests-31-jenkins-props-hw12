plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'guru.qa'
version = '1.0-SNAPSHOT'

def selenideVersion = "7.5.1",
    junitVersion = "5.11.3",
    allureVersion = "2.32.0",
    allureAdapterVersion = "2.29.0",
    allureSelenideVersion = "2.29.0",
    slf4jVersion = "2.0.16"

allure {
    report {
        version.set("$allureVersion")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("$allureAdapterVersion")
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation (
            "com.codeborne:selenide:$selenideVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.qameta.allure:allure-selenide:$allureSelenideVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "com.github.javafaker:javafaker:1.0.2"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task simple_test(type: Test) {
    useJUnitPlatform {
        includeTags("simple")
    }
}

task demoqa_test(type: Test) {
    useJUnitPlatform {
        includeTags("demoqa")
    }
}